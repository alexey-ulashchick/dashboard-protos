plugins {
	id 'com.google.protobuf' version '0.8.10'
	id 'com.jfrog.bintray' version '1.8.1'
	id 'java'
	id 'maven-publish'
}

def majorVersion = '1'
def minorVersion = '0'
def patchVersion = System.getenv("GITHUB_RUN_NUMBER") ?: '0'

group = 'com.ulashchick.dashboard'
version = majorVersion + '.' + minorVersion + '.' + patchVersion
sourceCompatibility = 1.8

sourceSets {
	main {
		proto {
			srcDir '../protos'
		}
	}
}

repositories {
//	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	// https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
	compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.10.0'

	// https://mvnrepository.com/artifact/io.grpc/grpc-all
	compile group: 'io.grpc', name: 'grpc-all', version: '1.25.0'

	// https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
	compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

	// https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxjava
	compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.20'

	// https://mvnrepository.com/artifact/com.salesforce.servicelibs/rxgrpc-stub
	compile group: 'com.salesforce.servicelibs', name: 'rxgrpc-stub', version: '1.0.1'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.10.1'
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.25.0'
		}
		rxgrpc {
			artifact = "com.salesforce.servicelibs:rxgrpc:1.0.1"
		}
	}
	generateProtoTasks {
		ofSourceSet("main")*.plugins {
			grpc { }
			rxgrpc {}
		}
	}
}

def pomConfig = {
	licenses {
		license {
			name 'MIT'
			url 'http://www.opensource.org/licenses/mit-license.php'
			distribution 'repo'
		}
	}
	developers {
		developer {
			id 'alexey-ulashchick'
			name 'Alexey Ulashchick'
			email 'a.ulashchick@gmail.com'
		}
	}
	scm {
		url 'https://github.com/alexey-ulashchick/dashboard-protos'
	}
}

publishing {
	publications {
		mavenPublication(MavenPublication) {
			from components.java
			groupId 'com.ulashchick.dashboard'
			artifactId 'protos'
			version = majorVersion + '.' + minorVersion + '.' + patchVersion
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'Protos library for Dashboard Project')
				root.appendNode('name', 'dashboard-protos')
				root.appendNode('url', 'https://github.com/alexey-ulashchick/dashboard-protos')
				root.children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = System.getenv('JFROG_USER')
	key = System.getenv('JFROG_API_KEY')
	publications = ['mavenPublication']
	pkg {
		repo = 'maven'
		name = 'dashboard-protos'
		licenses = ['MIT']
		vcsUrl = 'https://github.com/alexey-ulashchick/dashboard-protos.git'
	}
}
